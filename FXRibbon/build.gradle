plugins {
    id "java-library"
    id "maven-publish"
}

sourceCompatibility = '1.8'
group = 'com.pixelduke'
version = '1.3.0'
def versionDescription = 'FXRibbon'


[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
    manifest {
        attributes( 'Implementation-Title': project.name,
                    'Implementation-Version': project.version)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('javafx', '-quiet')
}

//artifacts {
//    archives sourceJar
//    archives javadocJar
//}

/* Publishing */
def pomConfig = {
    packaging 'jar'
    name project.name
    url 'https://pixelduke.com/fxribbon/'
    inceptionYear '2014'
    licenses {
        license([:]) {
            name 'BSD New'
            url 'https://opensource.org/licenses/BSD-3-Clause'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/JackRabbit156/FXRibbon.git'
    }
    developers {
        [
                dukke: 'Pedro Duque Vieira',
                JackRabbit156: 'Frank Rieger'
        ].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
//            artifact sourceJar {
//                classifier "sources"
//            }
//            artifact javadocJar {
//                classifier "javadoc"
//            }

            pom.withXml {
                asNode().children().last() + pomConfig
                asNode().appendNode('description', "Ribbon control for Java, created in JavaFX")
            }
        }
    }
}

